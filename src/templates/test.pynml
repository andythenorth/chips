cargotable {
	LVST
}

spriteset (cow_pen_empty, "src/graphics/cows_cargo.png") {
	[  10, 10, 64, 65, -31, -34 ]
	[ 220, 10, 64, 65, -31, -34 ]
}

spriteset (cow_pen_half, "src/graphics/cows_cargo.png") {
	[  80, 10, 64, 65, -31, -34 ]
	[ 290, 10, 64, 65, -31, -34 ]
}

spriteset (cow_pen_full, "src/graphics/cows_cargo.png") {
	[ 150, 10, 64, 65, -31, -34 ]
	[ 360, 10, 64, 65, -31, -34 ]
}

spritelayout cow_pen_X(a) {
	ground {
		sprite: 2022 + a; // prevent railtype offset
	}
	building {
		sprite: cow_pen_empty(0); // first sprite in active spriteset
		zextent: 36;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}

spritelayout cow_pen_Y(a) {
	ground {
		sprite: 2022 + a; // prevent railtype offset
	}
	building {
		sprite: cow_pen_empty(1); // second sprite in active spriteset
		zextent: 36;
		recolour_mode: RECOLOUR_REMAP;
		palette:       PALETTE_USE_DEFAULT;
	}
}

spritegroup cow_pen_1 {
	little: [cow_pen_empty, cow_pen_half];
	lots:   cow_pen_full;
}

spritegroup cow_pen_2 {
	little: [cow_pen_empty, cow_pen_half, cow_pen_full];
	lots:   cow_pen_full;
}

random_switch(FEAT_STATIONS, TILE, random_cow_pen) {
	1: cow_pen_1;
	1: cow_pen_2;
}

/* Define the station itself */
item(FEAT_STATIONS, cow_pen) {
	property {
		/* The class allows to sort stations into categories. */
		class:                  "NML_";
		/* If no other NewGRF provides this class before us, we have to name it */
		classname:              string(STR_NAME_STATCLASS);
		/* Name of this particular station */
		name:                   string(STR_NAME_STATION);
		cargo_threshold: 160;
		draw_pylon_tiles: 0;
		hide_wire_tiles: 0xFF;
		non_traversable_tiles: 0xFF;
	}
	graphics {
		sprite_layouts: [cow_pen_X(0), cow_pen_Y(0)];
		purchase: cow_pen_half;
		LVST: random_cow_pen;
		cow_pen_empty;
	}
}
